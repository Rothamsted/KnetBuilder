CONSTRUCT 
{
  ?concept 
    dcterms:identifier ?pid;
  	bk:prefName ?prefName;
  	bk:altName ?altName;    
  	dcterms:description ?description;
  	rdfs:comment ?annotation;
    bk:_conceptClassId ?ccId;
    bk:relatedConcept ?relatedConcept;
    bk:evidence ?evidence.
    
  ?evidence
    rdf:type bk:EvidenceType;
		dcterms:identifier ?evId;
    rdfs:label ?evLabel;
  	dcterms:description ?evDescription    
}
WHERE
{
	VALUES ( ?concept ) {
	  $resourceIris
	} 

	# TODO: continue with evidence
	#   1) JSON-LD framing, so that objects are nested (also, review relation types)
	#Â   2) change the templates.
	# TODO: datasources, accessions, evidence, attributes.
	
  ?concept rdf:type ?conceptClass.
  		
  ?conceptClass bk:isOndexPreferred true.
  ?conceptClass dcterms:identifier ?ccIdRaw.
  
  BIND ( IF ( ?ccIdRaw = 'Concept', 'Thing', ?ccIdRaw ) AS ?ccId )
     
  OPTIONAL { ?concept dcterms:identifier ?pid }
  OPTIONAL { ?concept bk:prefName ?prefName }
  OPTIONAL { ?concept bk:altName ?altName }
  OPTIONAL { ?concept dcterms:description ?description }
  OPTIONAL { ?concept rdfs:comment ?annotation }
  
  OPTIONAL { ?concept bk:relatedConcept ?relatedConcept }
  OPTIONAL { 
  	?concept bk:evidence ?evidence. 
		?evidence dcterms:identifier ?evId.
    OPTIONAL { ?evidence rdfs:label ?evLabel }
  	OPTIONAL { ?evidence dcterms:description ?evDescription }
  }
}
